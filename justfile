set shell := ["sh", "-cu"]
set windows-shell := ["sh", "-cu"]
set positional-arguments

export AMUX_GIT_SHA := `git rev-parse HEAD`

# Wall-clock bound for every recipe; portable where GNU timeout is absent.
bounded := "scripts/bounded"
# Desktop roots keep naming the feature explicitly so an invocation that
# disables defaults cannot silently switch the SQLite linkage policy.
desktop_features := "--features bundled"

# List the available repository tasks.
default:
    @just --list

# The iPhone app's recipes: `just ios build`, `just ios verify`, ... (`just --list ios`).
mod ios 'apps/apple/justfile'

# Build the desktop product binaries.
build:
    {{bounded}} 900 cargo build --locked -p amux --bins {{desktop_features}}

# Check every workspace library and binary.
check:
    {{bounded}} 900 cargo check --locked --workspace --lib --bins {{desktop_features}}

# Run workspace tests, preserving explicit Cargo target selections.
test *ARGS:
    if [ "${1-}" = -- ]; then shift; fi; scripts/workspace-test.sh "$@"

# Run tests for one named workspace crate.
test-crate CRATE *ARGS:
    crate=$1; shift; if [ "${1-}" = -- ]; then shift; fi; feature=; if grep -q '^bundled *=' "crates/$crate/Cargo.toml"; then feature='{{desktop_features}}'; fi; {{bounded}} 900 cargo test --locked -p "$crate" $feature "$@"

# Exercise the profile store against both of its implementations.
test-store *ARGS:
    if [ "${1-}" = -- ]; then shift; fi; {{bounded}} 1200 cargo test --locked -p store --features bundled "$@"

# Cross-compile every store test target and run it on the leased, booted iOS
# simulator, after proving the SQLite suite's binary carries SQLite itself.
test-store-ios:
    scripts/with iphone -- {{bounded}} 2300 scripts/python -B scripts/ios-store-test.py

# Exercise the pure store-backed reducer lifecycle and recorded UI specs.
test-ui *ARGS:
    if [ "${1-}" = -- ]; then shift; fi; {{bounded}} 1200 cargo test --locked -p ui-state "$@"

# Compile every ordinary workspace test target without running it.
test-build:
    {{bounded}} 1200 cargo test --locked --workspace --lib --tests --no-run {{desktop_features}}

# Run all workspace documentation tests.
doctest:
    {{bounded}} 600 cargo test --locked --workspace --doc {{desktop_features}}

# Run the whole-daemon and UI-state specification suites.
spec *ARGS:
    if [ "${1-}" = -- ]; then shift; fi; scripts/spec-test.sh "$@"

# Exercise TUI behavior.
test-tui *ARGS:
    if [ "${1-}" = -- ]; then shift; fi; {{bounded}} 1200 cargo test --locked -p tui --features fixtures "$@"

# Lint every workspace target with warnings denied.
lint:
    {{bounded}} 1200 cargo clippy --locked --workspace --all-targets {{desktop_features}} -- -D warnings

# Format all Rust sources with the pinned nightly toolchain.
fmt:
    {{bounded}} 600 cargo +nightly-2026-08-30 fmt --all

# Check Rust formatting with the pinned nightly toolchain.
fmt-check:
    {{bounded}} 600 cargo +nightly-2026-08-30 fmt --all -- --check

# Regenerate the committed protobuf bindings.
protobuf:
    {{bounded}} 600 cargo run --locked -p xtask -- codegen

# Check that committed protobuf bindings match their sources.
codegen-check:
    {{bounded}} 600 scripts/codegen-check.sh

# Build the shipping binary and enforce the release dependency policy.
release-check *ARGS:
    if [ "${1-}" = -- ]; then shift; fi; {{bounded}} 1200 cargo build --locked --release -p amux --bins --no-default-features {{desktop_features}} "$@"
    if [ "${1-}" = -- ]; then shift; fi; scripts/release-policy-check.sh "$@"

# `just release 0.8.0` writes the version, checks the release build, commits,
# tags v0.8.0 and pushes; the tag's workflow builds the binaries into a
# GitHub Release.
# Cut a release of the amux binary.
release VERSION:
    {{bounded}} 1800 cargo run --locked -q -p xtask -- release cut {{VERSION}}

# `just deploy 0.8.0` signs the release's binaries with the keychain's key and
# publishes the stable manifest; `--channel preview` and `--rollout 10`
# choose who takes it.
# Put a cut release in front of a channel.
deploy VERSION *ARGS:
    shift; if [ "${1-}" = -- ]; then shift; fi; {{bounded}} 3600 cargo run --locked -q -p xtask -- release deploy {{VERSION}} "$@"

# `just release-key generate` makes one in the login keychain and prints its
# public half; `just release-key public` prints it again.
# The release signing key.
release-key *ARGS:
    if [ "${1-}" = -- ]; then shift; fi; {{bounded}} 600 cargo run --locked -q -p xtask -- release key "$@"

# Check the provider-free graph used by embedded clients.
embedded-check:
    {{bounded}} 900 cargo check --locked -p node -p client -p ui-state -p ui-runtime -p app-runtime -p app-embedded

# Exercise the public embedded owner and client boundary.
embedded-test:
    {{bounded}} 900 cargo test --locked -p app-embedded --test embedded

# Check the provider-free graph for iOS devices and simulators.
mobile-check:
    {{bounded}} 1200 cargo check --locked -p node -p client -p ui-state -p ui-runtime --target aarch64-apple-ios
    {{bounded}} 1200 cargo check --locked -p node -p client -p ui-state -p ui-runtime --target aarch64-apple-ios-sim

# Run workspace tests with isolated user configuration and no external network.
# The workspace test script bounds its own compile and run phases.
offline-test:
    scripts/offline-check.sh scripts/workspace-test.sh --lib --tests

# Build the product with the full-debug profile.
full-debug:
    {{bounded}} 900 cargo build --locked -p amux --bins --profile full-debug {{desktop_features}}

# Render or inspect deterministic TUI evidence.
shot *ARGS:
    if [ "${1-}" = -- ]; then shift; fi; {{bounded}} 600 cargo run --locked --quiet -p shot --bin amux-shot -- "$@"

# Run one declared journey through its real client. System journeys are the
# built binaries on the fakes, printing their transcripts. `just journey
# terminal all` runs every terminal story, keeps going after a failure,
# prints one line per story to target/journeys/summary.txt and fails if any
# story did.
journey CLIENT NAME *ARGS:
    #!/usr/bin/env sh
    set -eu
    if [ "${3-}" = -- ]; then shift 3; else shift 2; fi
    case "{{CLIENT}}" in
    terminal)
        {{bounded}} 900 cargo build --locked -p amux -p provider-fakes -p testnet --bins {{desktop_features}}
        if [ "{{NAME}}" = all ]; then
            {{bounded}} 3000 scripts/python -B scripts/terminal-journeys.py
        else
            {{bounded}} 600 scripts/python -B scripts/terminal-journey.py "{{NAME}}"
        fi
        ;;
    system)
        # --keep leaves the install running for other clients to open.
        if [ "${1-}" = --keep ]; then export AMUX_JOURNEY_KEEP=1; fi
        {{bounded}} 1500 cargo test --locked -p amux --test system_journeys -- --exact --nocapture "$(echo "{{NAME}}" | tr - _)"
        ;;
    *)
        echo "no {{CLIENT}} journeys: terminal or system" >&2
        exit 2
        ;;
    esac

# Replay a headless Claude recording through the interpreter and print JSON:
# the snapshot count, the largest and median encoded snapshot, and per tool
# call the bytes its items and appends took. Offline and deterministic.
wire-size RECORDING:
    {{bounded}} 600 cargo run --locked --quiet -p replay-support --bin wire-size -- "{{RECORDING}}"

# Generate and verify the complete TUI evidence bundle.
tui-evidence *ARGS:
    if [ "${1-}" = -- ]; then shift; fi; {{bounded}} 1800 scripts/tui-evidence "$@"

# Serve a declared world from journeys/sets on real daemons and the fake
# providers, and run the real terminal client on it in this terminal; the
# door's address is in target/tui-set/NAME/door.json. `--frames KEYS
# [--size WxH]...` draws the client headlessly to text and PNG instead.
# `just tui-set list` lists the sets. The session is interactive, so only
# the build and frames are bounded.
tui-set NAME *ARGS:
    #!/usr/bin/env sh
    set -eu
    {{bounded}} 900 cargo build --locked -p amux -p provider-fakes -p testnet -p tui-set --bins {{desktop_features}}
    case " $* " in
    *" --frames"*) exec {{bounded}} 600 target/debug/tui-set "$@" ;;
    *) exec target/debug/tui-set "$@" ;;
    esac

# Enforce production and test-infrastructure dependency boundaries.
dependency-policy:
    {{bounded}} 60 scripts/py scripts/check-dependency-policy.py

# Warm the product and ordinary test build graphs used by new worktrees.
warm: build test-build

# Print the suite catalogue with its boundaries, lanes and focused recipes.
tests-list:
    scripts/python -B scripts/tests-catalog.py list

# Validate every catalogued recipe, target, feature, manifest and baseline.
tests-check:
    {{bounded}} 120 scripts/python -B scripts/tests-catalog.py check

# Fail when a contract in tests/contracts.toml names no test or a test that
# does not exist; `--table PATH` also writes the coverage table as Markdown.
contracts-check *ARGS:
    {{bounded}} 300 scripts/python -B scripts/contracts-check.py "$@"

# Fail when a docs link or image does not resolve, a figure is unreferenced
# or not standalone, or docs/README.md does not list every page once.
docs-check:
    {{bounded}} 120 scripts/py scripts/docs-check.py

# Fail if an interpreter or the agent's provider handshake builds or reads
# provider JSON by hand instead of through the protocol crates.
typed-provider-check:
    {{bounded}} 60 scripts/py scripts/typed-provider-check.py

# Fail when code, config, protos, recipes, scripts, workflows or docs still
# name a mechanism amux removed.
deletion-ledger-check:
    {{bounded}} 120 scripts/py scripts/deletion-ledger-check.py

# Refuse fixture-regeneration flags before any asserted CI check runs.
[private]
no-update-flags:
    scripts/no-update-flags.sh

# Run the same task sequence exercised across continuous-integration jobs.
ci: no-update-flags check lint fmt-check codegen-check dependency-policy typed-provider-check deletion-ledger-check docs-check tests-check test contracts-check doctest release-check embedded-check embedded-test mobile-check

# Run the live provider compatibility lane for one kind (claude_pty,
# claude_sdk or codex) and scenario (initialize, respond, decide, interrupt,
# resume, plan, questions, usage, catalogue, host_catalogue, permission, mode,
# attach, or all), under the operator's existing login. With no arguments
# it reports not_run for every kind and starts nothing.
live *ARGS:
    set -e; if [ "${1-}" = -- ]; then shift; fi; if [ $# -eq 0 ]; then for kind in claude_pty claude_sdk codex; do echo "live $kind: not_run (no scenario selected)"; done; exit 0; fi; kind=$1; shift; case "$kind" in claude_pty|claude_sdk|codex) ;; *) echo "live: unknown kind $kind; known: claude_pty, claude_sdk, codex" >&2; exit 2 ;; esac; {{bounded}} 3600 cargo test --locked -p qualification --features bundled,live --test "${kind}_live" -- "$@"

# Qualify desktop performance on an enrolled machine. Pass --baseline to
# record the current release medians after every absolute budget passes.
perf *ARGS:
    set -e; if [ "${1-}" = -- ]; then shift; fi; {{bounded}} 1200 cargo build --locked --release -p qualification --bin perf --features bundled,perf; {{bounded}} 1800 target/release/perf "$@"
